import DocCard from '@theme/DocCard';
import {findSidebarItem} from '@site/src/sidebarUtils';

# Quickstart

<p style={{width: '100%'}}>
    <iframe style={{margin: 'auto', display:'block'}} width="560" height="315" src="https://www.youtube-nocookie.com/embed/LDLz06X_KNY" title="Deploying a Smart Contract on Oasis Sapphire: A Tutorial" allow="accelerometer; autoplay; clipboard-write; encrypted-media; gyroscope; picture-in-picture; web-share" allowFullScreen></iframe>
</p>

In this tutorial, you will build and deploy a unique dApp that requires
confidentiality to work. By the end of the tutorial, you should feel
comfortable setting up your EVM development environment to target Sapphire,
and know how and when to use confidentiality.

The expected completion time of this tutorial is 15 minutes.

## Create a Sapphire-Native dApp

Porting an existing EVM app is cool, and will provide benefits such as
protection against MEV.
However, starting from scratch with confidentiality in mind can unlock some
really novel dApps and provide a [higher level of security].

One simple-but-useful dApp that takes advantage of confidentiality is a
[dead person's switch] that reveals a secret (let's say the encryption key to a
data trove) if the operator fails to re-up before too long.
Let's make it happen!

[higher level of security]: ./develop/README.mdx
[dead person's switch]: https://en.wikipedia.org/wiki/Dead_man%27s_switch

### Init a new Hardhat project

We're going to use Hardhat, but Sapphire should be compatible with your dev
environment of choice. Let us know if things are not as expected!

1. Make & enter a new directory:

```sh
mkdir quickstart && cd quickstart
```

2. Create a TypeScript project and install the project dependencies:

```sh
npx hardhat init
```

3. Add [`@oasisprotocol/sapphire-hardhat`] as dependency:

```shell npm2yarn
npm install -D @oasisprotocol/sapphire-hardhat
```

### Add the Sapphire Testnet to Hardhat

Open up your `hardhat.config.ts` and drop in these lines.

  ```js title="hardhat.config.ts"
  import { HardhatUserConfig } from "hardhat/config";
  // highlight-next-line
  import '@oasisprotocol/sapphire-hardhat';
  import "@nomicfoundation/hardhat-toolbox";

  const config: HardhatUserConfig = {
   solidity: "0.8.19",
   // highlight-start
   networks: {
     'sapphire-testnet': {
       // This is Testnet! If you want Mainnet, add a new network config item.
       url: "https://testnet.sapphire.oasis.io",
       accounts: process.env.PRIVATE_KEY
         ? [process.env.PRIVATE_KEY]
         : [],
       chainId: 0x5aff,
     },
   },
   // highlight-end
  };

  export default config;
  ```

By importing `@oasisprotocol/sapphire-hardhat`, **any network config entry
corresponding to the Sapphire's chain ID will automatically be wrapped with
Sapphire specifics for encrypting and signing the transactions**.

### Get some Sapphire Testnet tokens

Now for the fun part. As you have configured the Sapphire Test network, get some native TEST tokens.
Hit up the one and only [Oasis Testnet faucet], select "Sapphire" and enter your address.
Submit the form and TEST be on your way.

[Oasis Testnet faucet]: https://faucet.testnet.oasis.io

### Get the Contract

This is a Sapphire tutorial and you're already a Solidity expert, so let's not
bore you with explaining the gritty details of the contract.
Start by pasting `Vigil.sol` into `contracts/Vigil.sol`.

1. Create a new file called `Vigil.sol` under `contracts/`:
2. Paste the following contract into it:

<details>
  <summary> Vigil.sol contract </summary>

  ```solidity title="contracts/Vigil.sol" showLineNumbers
  // SPDX-License-Identifier: MIT
  pragma solidity ^0.8.9;

  contract Vigil {
      struct SecretMetadata {
          address creator;
          string name;
          /// @notice How long (in seconds) the secret should remain so past the creator's last update.
          uint256 longevity;
      }

      event SecretCreated(
          address indexed creator,
          string indexed name,
          uint256 index
      );
      event SecretRevealed(
          address indexed creator,
          string indexed name,
          uint256 index
      );

      SecretMetadata[] public _metas;
      bytes[] private _secrets;
      /// @dev The unix timestamp at which the address was last seen.
      mapping(address => uint256) public _lastSeen;

      function createSecret(
          string calldata name,
          uint256 longevity,
          bytes calldata secret
      ) external {
          _updateLastSeen();
          _metas.push(
              SecretMetadata({
                  creator: msg.sender,
                  name: name,
                  longevity: longevity
              })
          );
          _secrets.push(secret);
          emit SecretCreated(msg.sender, name, _metas.length - 1);
      }

      /// @notice Reveals the secret at the specified index.
      function revealSecret(uint256 index) external returns (bytes memory) {
          require(index < _metas.length, "no such secret");
          address creator = _metas[index].creator;
          uint256 expiry = _lastSeen[creator] + _metas[index].longevity;
          require(block.timestamp >= expiry, "not expired");
          emit SecretRevealed(creator, _metas[index].name, index);
          return _secrets[index];
      }

      /// @notice Returns the time (in seconds since the epoch) at which the owner was last seen, or zero if never seen.
      function getLastSeen(address owner) external view returns (uint256) {
          return _lastSeen[owner];
      }

      function getMetas(uint256 offset, uint256 count)
          external
          view
          returns (SecretMetadata[] memory)
      {
          if (offset >= _metas.length) return new SecretMetadata[](0);
          uint256 c = offset + count <= _metas.length
              ? count
              : _metas.length - offset;
          SecretMetadata[] memory metas = new SecretMetadata[](c);
          for (uint256 i = 0; i < c; ++i) {
              metas[i] = _metas[offset + i];
          }
          return metas;
      }

      function refreshSecrets() external {
          _updateLastSeen();
      }

      function _updateLastSeen() internal {
          _lastSeen[msg.sender] = block.timestamp;
      }
  }
  ```
</details>

3. Create a new file named `run-vigil.ts` into `scripts/run-vigil.ts`
4. Paste the following contract into it:

<details>
  <summary> run-vigil.ts script </summary>

  ```js title="scripts/run-vigil.ts" showLineNumbers
  // Usage: pnpm hardhat run --network <network> scripts/run-vigil.ts

  import { ethers } from 'hardhat';

  async function main() {
    console.log((await ethers.provider.getNetwork()).name);
    const Vigil = await ethers.getContractFactory('Vigil');
    const vigil = await Vigil.deploy();
    console.log('Vigil deployed to:', await vigil.getAddress());

    const tx = await vigil.createSecret(
      'ingredient',
      30 /* seconds */,
      Buffer.from('brussel sprouts'),
    );
    console.log('Storing a secret in', tx.hash);
    await tx.wait();
    try {
      console.log('Checking the secret');
      await vigil.connect(ethers.provider).revealSecret.staticCall(0);
      console.log('Uh oh. The secret was available!');
      process.exit(1);
    } catch (e: any) {
      console.log('failed to fetch secret:', e.message);
    }
    console.log('Waiting...');

    await new Promise((resolve) => setTimeout(resolve, 30_000));
    console.log('Checking the secret again');
    await (await vigil.revealSecret(0)).wait(); // Reveal the secret.
    const secret = await vigil.revealSecret.staticCallResult(0); // Get the value.
    console.log('The secret ingredient is', Buffer.from(secret[0].slice(2), 'hex').toString());
  }

  main().catch((error) => {
    console.error(error);
    process.exitCode = 1;
  });
  ```
</details>

#### Vigil.sol, the interesting parts

The key state variables are:

```solidity
    SecretMetadata[] public _metas;
    bytes[] private _secrets;
```

* `_metas` is marked with `public` visibility, so despite the state itself being
  encrypted and not readable directly, Solidity will generate a getter that will
  do the decryption for you.
* `_secrets` is `private` and therefore truly secret; only the contract can
  access the data contained in this mapping.

And the methods we'll care most about are

* `createSecret`, which adds an entry to both `_metas` and `_secrets`.
* `revealSecret`, which acts as an access-controlled getter for the data
  contained with `_secrets`. Due to trusted execution and confidentiality, the
  only way that the secret will get revealed is if execution proceeds all the
  way to the end of the function and does not revert.

The rest of the methods are useful if you actually intended to use the contract,
but they demonstrate that developing for Sapphire is essentially the same as for
Ethereum.
You can even write tests against the Hardhat network and use Hardhat plugins.

### Run the Contract

And to wrap things up, we'll put `Vigil` through its paces.
First, let's see what's actually going on.

After deploying the contract, we can create a secret, check that it's not
readable, wait a bit, and then check that it has become readable.
Pretty cool if you ask me!

Anyway, make it happen by running

```shell
PRIVATE_KEY="0x..." npx hardhat run scripts/run-vigil.ts --network sapphire-testnet
```

And if you see something like the following, you'll know you're well on the road
to deploying confidential dApps on Sapphire.

```
Vigil deployed to: 0x74dC4879B152FDD1DDe834E9ba187b3e14f462f1
Storing a secret in 0x13125d868f5fb3cbc501466df26055ea063a90014b5ccc8dfd5164dc1dd67543
Checking the secret
failed to fetch secret: reverted: not expired
Waiting...
Checking the secret again
The secret ingredient is brussel sprouts
```

## All done!

Congratulations, you made it through the Sapphire tutorial! If you want to dive
deeper, please check out the [develop] chapter and join the discussion on the
[#dev-central Discord channel][social-media].

Best of luck on your future forays into confidentiality!

:::info Example: Starter project

If your project involves building a web frontend, we recommend that you check
out the official [Oasis starter] files.

[Oasis starter]: https://github.com/oasisprotocol/demo-starter

:::

:::info Example: Go and Python

Are you building your dApp in languages other than TypeScript? Check out the
official [Oasis starter project for Go] and the [Oasis starter project for Python].

[Oasis starter project for Go]: https://github.com/oasisprotocol/demo-starter-go
[Oasis starter project for Python]: https://github.com/oasisprotocol/demo-starter-py

:::

## See also

<DocCard item={findSidebarItem('/build/sapphire/develop/browser')} />
<DocCard item={findSidebarItem('/node/run-your-node/paratime-client-node')} />
<DocCard item={findSidebarItem('/node/web3')} />

[social-media]: https://github.com/oasisprotocol/docs/blob/main/docs/get-involved/README.md#social-media-channels
[develop]: ./develop/README.mdx
[hardhat-example]: https://github.com/oasisprotocol/sapphire-paratime/blob/stable/clients/js/1.x/examples/hardhat
[`@oasisprotocol/sapphire-hardhat`]: https://www.npmjs.com/package/@oasisprotocol/sapphire-hardhat
